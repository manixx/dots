pkgname=st
pkgver=0.8.4
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=(libxft)
url=https://st.suckless.org
source=(
	https://dl.suckless.org/$pkgname/$pkgname-$pkgver.tar.gz
	https://st.suckless.org/patches/bold-is-not-bright/st-bold-is-not-bright-20190127-3be4cf1.diff
	https://st.suckless.org/patches/boxdraw/st-boxdraw_v2-0.8.3.diff
)
sha256sums=(
	'd42d3ceceb4d6a65e32e90a5336e3d446db612c3fbd9ebc1780bc6c9a03346a6'
	'329169acac7ceaf901995d6e0897913089b799d8cd150c7f04c902f4a5b8eab2'
	'a24118148631f6670ea568a3debdd00a7cbcfa525839281888e876e7ea409658'
)
_sourcedir=$pkgname-$pkgver
_makeopts="--directory=$_sourcedir"
_patchopts="--directory=$_sourcedir -p1 --forward -i"

prepare() {
	patch ${_patchopts} ${BUILDDIR}/st-bold-is-not-bright-20190127-3be4cf1.diff || true 
	patch ${_patchopts} ${BUILDDIR}/st-boxdraw_v2-0.8.3.diff || true

  cp "${BUILDDIR}/config.h" "${_sourcedir}"
}

build() {
  make $_makeopts 
}

package() {
  local installopts='--mode 0644 -D --target-directory'
  local shrdir="$pkgdir/usr/share"
  local licdir="$shrdir/licenses/$pkgname"
  local docdir="$shrdir/doc/$pkgname"
  make $_makeopts PREFIX=/usr DESTDIR="$pkgdir" install
  install $installopts "$licdir" "$_sourcedir/LICENSE"
  install $installopts "$docdir" "$_sourcedir/README"
  install $installopts "$shrdir/$pkgname" "$_sourcedir/st.info"
}
